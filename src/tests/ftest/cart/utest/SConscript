# (C) Copyright 2016-2021 Intel Corporation.
#
# SPDX-License-Identifier: BSD-2-Clause-Patent
#
"""Unit tests"""

import os

TEST_SRC = ['test_linkage.cpp', 'utest_hlc.c', 'utest_swim.c', 'utest_portnumber.c']
LIBPATH = [Dir('../../'), Dir('../../../gurt')]


def scons():
    """Scons function"""
    if GetOption('help'):
        return

    Import('env', 'prereqs', 'cart_targets', 'swim_targets', 'gurt_targets')

    # Use full path to wrap_cmocka.h for configure test.  Since standalone
    # cmocka header can't be included without including other headers,
    # this test can't be generalized
    env.AppendUnique(CPPPATH=[Dir('.').srcnode()])
    wrap_cmocka = os.path.join(Dir('.').srcnode().abspath, 'wrap_cmocka.h')
    prereqs.define('cmockawrap', headers=[wrap_cmocka], libs=['cmocka'],
                   package='libcmocka-devel')
    if not prereqs.check_component('cmocka'):
        print("\n***************************************************")
        print("libcmocka-devel package needed to enable unit tests")
        print("***************************************************\n")
        # Just returning is the desired behavior here.  The utest target
        # has yet to be defined so the build will just fail.
        return

    test_env = env.Clone()
    test_env.d_add_requires("mercury", "uuid", "cmocka")
    test_env.AppendUnique(LIBS=['pthread', 'm', 'yaml'])
    test_env.AppendUnique(CXXFLAGS=['-std=c++0x'])
    test_env.AppendUnique(LIBPATH=LIBPATH)
    test_env.AppendUnique(RPATH_FULL=LIBPATH)
    test_env['LINKFLAGS'] = []

    for test in TEST_SRC:
        Default(test_env.d_test_program([File(test), cart_targets, swim_targets, gurt_targets]))


if __name__ == "SCons.Script":
    scons()
