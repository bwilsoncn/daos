"""Build DAOS client"""

LIBDAOS_SRC = ['agent.c', 'array.c', 'container.c', 'event.c', 'init.c', 'job.c', 'kv.c', 'mgmt.c',
               'object.c', 'pool.c', 'rpc.c', 'task.c', 'tx.c']


def scons():
    """Execute build"""
    Import('env', 'API_VERSION', 'libdaos_tgts')

    env.d_add_build_rpath()
    env.AppendUnique(LIBPATH=[Dir('.')])

    denv = env.Clone()
    denv.d_add_requires('protobufc')
    libdaos_tgts[:0] = denv.SharedObject(LIBDAOS_SRC)

    if env.d_client_requested():
        daos_lib = denv.d_library('daos', libdaos_tgts, SHLIBVERSION=API_VERSION,
                                  LIBS=['daos_common'])
        denv.InstallVersionedLib('$PREFIX/lib64/', daos_lib, SHLIBVERSION=API_VERSION)
        Export('daos_lib')

    if env.d_tests_requested():
        SConscript('tests/SConscript', 'denv')


if __name__ == "SCons.Script":
    scons()
